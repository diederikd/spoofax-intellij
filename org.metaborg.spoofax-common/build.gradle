// This is a subproject of org.metaborg.intellij

intellij {
    type = "JPS"
}

buildConfig {
    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'org.metaborg.intellij'
    charset = 'UTF-8'

    buildConfigField 'String', 'METABORG_VERSION', metaborgVersion
}

configurations {
    language
    languageBaseline
    testLanguage
    testLanguageBaseline
}

task copyLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the resources.') {
    into "src/main/resources/languages/"
    from configurations.language
}

task copyTestLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.') {
    into "src/test/resources/languages/"
    from configurations.testLanguage
}

task copyLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the resources.', dependsOn: copyLanguageDependencies) {
    into "src/main/resources/languages/"
    from configurations.languageBaseline
}

task copyTestLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.', dependsOn: copyTestLanguageDependencies) {
    into "src/test/resources/languages/"
    from configurations.testLanguageBaseline
}

processResources.dependsOn copyLanguageBaselineDependencies
processTestResources.dependsOn copyTestLanguageBaselineDependencies

dependencies {
    compile     project(path: ":org.metaborg.spoofax-deps", configuration: "shadow")

    compile     "com.google.guava:guava:$guavaVersion"
    compile     "org.slf4j:slf4j-api:$slf4jVersion"

    compile     "com.google.code.findbugs:jsr305:$findbugsVersion"
    compile     "org.apache.commons:commons-lang3:$commonsLang3Version"
    compile     "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    compile     "com.google.inject:guice:$guiceVersion"
    compile     "com.google.inject.extensions:guice-assistedinject:$guiceVersion"
    compile     "com.google.inject.extensions:guice-multibindings:$guiceVersion"
    compile     "org.javassist:javassist:$javassistVersion"

    testCompile "junit:junit:4.12"

    language    "org.metaborg:dynsem:$metaborgVersion@spoofax-language"
    language    "org.metaborg:dynsem2coq:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.lang.coq:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.aterm:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.esv:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.nabl:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.nabl2.shared:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.nabl2.lang:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.nabl2.runtime:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.stratego:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.template:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.ts:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.spt.interactive:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lib.analysis:$metaborgVersion@spoofax-language"
    language    "org.metaborg:meta.lib.spoofax:$metaborgVersion@spoofax-language"
    language    "org.metaborg:org.metaborg.meta.lang.analysis:$metaborgVersion@spoofax-language"

    testLanguage "org.metaborg:org.metaborg.meta.lang.sdf:$metaborgVersion@spoofax-language"
}
