// This is a subproject of org.metaborg.intellij

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

assemble.dependsOn shadowJar
jar.enabled = false

task(printShadowDependencies, description: 'Prints all included shadow dependencies.') {
    doLast {
    	  def r = shadowJar.getIncludedDependencies()
    	  r.collect { it.name }.sort().each { dep ->
            println "      ${dep}"
        }
    }
}

task artifacts {
    group = "Help"
    description = "Displays the artifacts associated with each configuration of " + project
    doFirst {
        configurations.findAll().each { config ->
            println "${config}:"
            config.allArtifacts.getFiles().each { file -> println "" + file}
            println ' '
        }
    }
}

dependencies {
    // Main dependencies
    compile     ("org.metaborg:org.metaborg.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile     ("org.metaborg:org.metaborg.spoofax.meta.core:$metaborgVersion") {
        exclude group: "org.slf4j"
        exclude group: "com.google.guava"
    }
    compile "commons-io:commons-io:$commonsIoVersion"
}

shadowJar {
    baseName = 'org.metaborg.spoofax-deps'
    classifier = null
    zip64 = true

    relocate 'org.apache.commons.io', 'intellij.org.apache.commons.io'
}
